# Figure 2: PCA
DATASETS = ["bugeon", "bakken", "tosches", "tasic", "colquitt"]
# TODO: save e.g. gene lists to scratchpath 
species = {'colquitt': "Zebra finch", "tosches": "Turtle", 
            "tasic": "Mouse", "bugeon": "Mouse L1-3", 
            "bakken": "Human"}

rule figure2:
    input:
        expand("figures/figure2/pca_{dataset}.png", 
        dataset=[dataset for dataset in DATASETS if dataset!="bugeon"])

rule pca:
    input:
        raw_anndata = "data/anndata/{dataset}.h5ad",
        shared_genes="results/pandas/shared_genes.txt"
    params:
        species = lambda wildcards : species[wildcards.dataset]
    output:
        figure = "figures/figure2/pca_{dataset}.png",
        anndata = "results/anndata/{dataset}.h5ad",
    script:
        "fig2_pca.py"

rule intersect_genes:
    input:
        expand("results/pandas/genes_{dataset}.csv", 
        dataset=[dataset for dataset in DATASETS if dataset!="bugeon"]),
    output:
        shared_genes="results/pandas/shared_genes.txt",
    script:
        "fig2_intersect_genes.py"


rule datasets_table:
    input:
        expand("results/pandas/overview_{dataset}.csv", dataset=DATASETS),
    output:
        table="results/pandas/overview.csv",
        latex="results/pandas/overview.tex",
    script:
        "fig2_dataset_table.py"


rule datasets:
    input:
        anndata="data/anndata/{dataset}.h5ad",
    output:
        figure="figures/figure2/QC_{dataset}.png",
        table="results/pandas/overview_{dataset}.csv",
        genes="results/pandas/genes_{dataset}.csv",
    params:
        dataset=lambda wildcards: wildcards.dataset,
    script:
        "fig2_dataset_stats.py"
